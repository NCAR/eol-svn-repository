#! /usr/bin/perl -w

##Module-----------------------------------------------------------
# <p>The UVwinds script is a CGI script that provides a web
# interface for the UV wind calculation in the conversion
# modules.</p>
#
# @author Joel Clawson
# @version 3.0 (Version number used to match conversion version.)
# <p>Refactored the original web interface into individual conversion
# scripts and a super WebInterface module that the scripts inherit
# from.</p>
# <p>Updated to Version 3 of the conversion modules.</p>
#
# @author Joel Clawson
# @version 1.0 Original Creation.
##Module-----------------------------------------------------------
package UVwind;
use strict;
use lib "..";
use lib "/net/work/lib/perl/Utilities";
use DpgCalculations qw(:DEFAULT);
use DpgConversions qw(:DEFAULT);
use WebInterface;
our @ISA = ("WebInterface");

main();

##------------------------------------------------------------------
# @signature void main()
# <p>Run the script that creates the UV wind calculations web 
# interface.</p>
##------------------------------------------------------------------
sub main {
    my $self = UVwind->new();
    $self->makePage();    
}

##------------------------------------------------------------------
# @signature String createForm()
# <p>Create the HTML that defines the input form for the calculation.</p>
# @output $form The HTML code.
##------------------------------------------------------------------
sub createForm {
    my $self = shift;
    my $form = "<table><tbody>\n";

    # Make the wind speed line
    $form .= sprintf("<tr><td class=param>%s</td><td class=field>%s</td><td class=unit>%s</td></tr>\n","Wind Speed:",$self->textfield("windspd"),$self->getVelocityUnits("windspd_unit"));

    # Make the wind direction line.
    $form .= sprintf("<tr><td class=param>%s</td><td class=field>%s</td><td class=unit>%s</td></tr>","Wind Direction:",$self->textfield("winddir"),"&deg;");

    # Make the U and V component rows.
    my ($uwind,$vwind) = $self->getValues();
    $form .= sprintf("<tr class=result_line><td class=param>%s</td><td class=field>%s</td><td class=unit rowspan=2>%s</td></tr>\n","U Component:",$uwind,$self->getVelocityUnits("comp_unit"));
    $form .= sprintf("<tr class=result_line><td class=param>%s</td><td class=field>%s</td></tr>\n","V Component:",$vwind);

    $form .= sprintf("<tr><td class=submit colspan=3>%s</td></tr>\n",$self->submit("Calculate"));

    $form .= "</tbody></table>\n";

    return $form;
}

##------------------------------------------------------------------
# @signature String getCGI()
# <p>Get the name of the script to call when the form is submitted.</p>
# @output $cgi The script name.
##------------------------------------------------------------------
sub getCGI { return "calculations/uvwinds"; }

##------------------------------------------------------------------
# @signature String getEquationPage()
# <p>Get the name of the web page that contains the equations for
# the calculation.</p>
# @output $page The equation web page.
##------------------------------------------------------------------
sub getEquationPage { "equations_calc.html#winds"; }

##------------------------------------------------------------------
# @signature String getInstructions()
# <p>Get the HTML containing the instructions on how to use the 
# script.</p>
# @output $instr The HTML instructions.
##------------------------------------------------------------------
sub getInstructions {
    my $self = shift;
    my $instr = "<p>Insert the wind speed and direction values along with the appropriate units of the values.  Update the units for the output of the U and V components and press the <b>Calculate</b> button.  The values will appear in the table between the name and unit.</p>\n";
    $instr .= "<p>The warnings have been turned off for the calculation.  If no result appears, it is because a value was entered into one of the fields that does not allow the calculation to complete correctly.</p>\n";
    $instr .= "<p class=warning>The calculation requires the wind speed to be in meters/second.  The returned component values are in meters/second.  Any other units that are chosen will convert the value to the appropriate unit for the calculation and the output.</p>\n";
    return $instr;
}

##------------------------------------------------------------------
# @signature String getOnSubmit()
# <p>Get the javascript command to run when the form is submitted.</p>
# @output $js The javascript command.
##------------------------------------------------------------------
sub getOnSubmit { return "javascript: return (checkRhWindSpd('Wind Speed',windspd) && checkWindDirection('Wind Direction',winddir));"; }

##------------------------------------------------------------------
# @signature String getTitle()
# <p>Get the title of the script.</p>
# @output $title The script title.
##------------------------------------------------------------------
sub getTitle { return "Calculate U and V Wind Components"; }

##------------------------------------------------------------------
# @signature String getValues()
# <p>Get the calculated values or the &amp;nbsp; character when it
# cannot be calculated.</p>
# @output $value The float formated value.
##------------------------------------------------------------------
sub getValues {
    my $self = shift;
    if (defined($self->param("windspd")) && defined($self->param("winddir"))) {
	my ($u,$v) = calculateUVfromWind(convertVelocity($self->param("windspd"),
							 $self->param("windspd_unit"),
							 "m/s"),
					 $self->param("winddir"),0);
	
	if (defined($u) && defined($v)) {
	    return (sprintf("%f",convertVelocity($u,"m/s",$self->param("comp_unit"))),
		    sprintf("%f",convertVelocity($v,"m/s",$self->param("comp_unit"))));
	}
    }
    return ("&nbsp;","&nbsp;");
}

