#! /usr/bin/perl -w

##Module-----------------------------------------------------------
# <p>The Julian script is a CGI script that provides a web
# interface for the dew point calculation in the conversion
# modules.</p>
#
# @author Joel Clawson
# @version 3.0 (Version number used to match conversion version.)
# <p>Refactored the original web interface into individual conversion
# scripts and a super WebInterface module that the scripts inherit
# from.</p>
# <p>Updated to Version 3 of the conversion modules.</p>
#
# @author Joel Clawson
# @version 1.0 Original Creation.
##Module-----------------------------------------------------------
package Julian;
use strict;
use lib "..";
use lib "/work/software/conversion_modules/Version3";
use DpgDate qw(:DEFAULT);
use WebInterface;
our @ISA = ("WebInterface");

main();

##------------------------------------------------------------------
# @signature void main()
# <p>Run the script that creates the dew point calculations web 
# interface.</p>
##------------------------------------------------------------------
sub main {
    my $self = Julian->new();
    $self->makePage();    
}

##------------------------------------------------------------------
# @signature String createForm()
# <p>Create the HTML that defines the input form for the calculation.</p>
# @output $form The HTML code.
##------------------------------------------------------------------
sub createForm {
    my $self = shift;
    my $form = "<table><tbody>\n";

    # Make the entry row.
    $form .= sprintf("<tr><td class=param>%s</td><td class=field>%s</td><td class=unit>%s</td></tr>","Date:",$self->textfield("in_date"),$self->textfield(-name => "in_format",-default => "YYYY-JJJ"));

    # Make the result row.
    $form .= sprintf("<tr class=result_line><td class=param>%s</td><td class=field>%s</td><td class=unit>%s</td></tr>","Result:",$self->getValue(),$self->textfield(-name => "out_format",-default => "YYYY/MM/DD"));

    $form .= sprintf("<tr><td class=submit colspan=3>%s</td></tr>\n",$self->submit("Calculate"));

    $form .= "</tbody></table>\n";

    return $form;
}

##------------------------------------------------------------------
# @signature String getCGI()
# <p>Get the name of the script to call when the form is submitted.</p>
# @output $cgi The script name.
##------------------------------------------------------------------
sub getCGI { return "other/julian"; }

##------------------------------------------------------------------
# @signature String getEquationPage()
# <p>Get the name of the web page that contains the equations for
# the calculation.</p>
# @output $page The equation web page.
##------------------------------------------------------------------
sub getEquationPage { "equations_calc.html#dates"; }

##------------------------------------------------------------------
# @signature String getInstructions()
# <p>Get the HTML containing the instructions on how to use the 
# script.</p>
# @output $instr The HTML instructions.
##------------------------------------------------------------------
sub getInstructions {
    my $self = shift;
    my $instr = "<p>Insert a date into the field along with its format.  Insert the output format on the result line for the desired output format for the date.  The format recognizes the following characters as part of the format:<ul><li><b>Y</b> - Year</li><li><b>M</b> - Month</li><li><b>D</b> - Day</li><li><b>J</b> - Julian Day</li></ul></p>\n";
    return $instr;
}

##------------------------------------------------------------------
# @signature String getOnSubmit()
# <p>Get the javascript command to run when the form is submitted.</p>
# @output $js The javascript command.
##------------------------------------------------------------------
sub getOnSubmit { return "javascript: return true;"; }

##------------------------------------------------------------------
# @signature String getTitle()
# <p>Get the title of the script.</p>
# @output $title The script title.
##------------------------------------------------------------------
sub getTitle { return "Convert Date Formats"; }

##------------------------------------------------------------------
# @signature String getValue()
# <p>Get the calculated value or the &amp;nbsp; character when it
# cannot be calculated.</p>
# @output $value The float formated value.
##------------------------------------------------------------------
sub getValue {
    my $self = shift;
    if (defined($self->param("in_date"))) {
	my $value = formatDate($self->param("in_date"),$self->param("in_format"),$self->param("out_format"));
	return $value if (defined($value));
    }
    return "&nbsp;";
}















